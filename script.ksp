
{***********************************************
Change Pitch with Note Range & 1st note Timing
Author: Native Instruments
Written by: Nicki Marinic, Dominik Svoboda
Modified: October 18, 2022
*************************************************}
on init
    make_perfview
    
    {****Range & Change pitch****}
    
    declare ui_switch $learn_button
    declare ui_switch $pitch_bypass
	set_text($learn_button,"Learn Keyrange")
    set_text($pitch_bypass,"Bypass Pitch")
	declare $learn_counter
	declare $a
    declare ui_label $label_1 (1,1)
	set_text ($label_1,"Note range")
    declare ui_value_edit $Min (0,127,$VALUE_EDIT_MODE_NOTE_NAMES)
  	$Min := 24
    declare ui_value_edit $Max (0,127,$VALUE_EDIT_MODE_NOTE_NAMES)
  	$Max := 48
    set_script_title("Change Pitch w/ Range")
	set_ui_height(2)
	message("")
	declare $min_after_transpose
	declare $max_after_transpose
	
	declare polyphonic $transposition {transposition interval in Semitones}
	declare polyphonic $transposed_note {note number of transposed note}
    
	declare ui_knob $Semi (-11, 11, 1)
	$Semi := 0
	set_knob_defval ($Semi,0)
	make_persistent ($Semi)
    read_persistent_var ($Semi)
	set_knob_unit ($Semi,$KNOB_UNIT_ST)
	move_control ($Semi,3,3)
	set_control_help ($Semi,"Semi: Transposes incoming MIDI notes in semitones.")
	
	declare ui_knob $Octave (-10, 10, 1)
	$Octave := 0
	set_knob_defval ($Octave,0)
	set_knob_unit ($Octave,$KNOB_UNIT_OCT)
	move_control ($Octave, 2, 3)
	set_control_help ($Octave,"Octave: Transposes incoming MIDI notes in octaves.")
	
	declare ui_label $label1 (1,1)
	set_text ($label1,"Transpose")
	move_control ($label1,1,3)
	
	declare ui_label $MIDI_label (3,1)
	set_text ($MIDI_label,"MIDI")
	move_control ($MIDI_label,1,2)
	move_control ($pitch_bypass, 4,4)

	{**** Velocity to CC1 ****}

	declare ui_switch $V_C
	set_text($V_C,"Velocity to CC1")
	make_persistent ($V_C)
	move_control ($V_C, 4,3)


    {****Timing****}
    
    declare ui_switch $time_bypass
    declare $i
    declare $n
    declare $pressed
	declare polyphonic $Delayed_Note
    $i := 0
    $n := 0
    $pressed := 0
    declare ui_label $info (1,1)
	move_control ($info,5,1)
    set_text ($info, "Timing in (ms):")
	set_text ($time_bypass, "Bypass Timing")
	move_control ($time_bypass,6,1)
    declare ui_knob $1st_note (0,200,1)
	move_control ($1st_note,5,2)
    $1st_note := 0

    make_persistent ($1st_note)
    make_persistent ($time_bypass)
    make_persistent ($pitch_bypass)
    make_persistent ($Min)
    make_persistent ($Max)
	make_persistent ($Octave)
    make_persistent ($min_after_transpose)
    make_persistent ($max_after_transpose)
end on

on ui_control ($Min)
	if ($Min > $Max)
		$Max := $Min
	end if
end on

on ui_control ($Max)
	if ($Max < $Min)
		$Min := $Max
	end if
end on

on ui_control ($Octave)
	if ($Octave < 0)
	$max_after_transpose := $Max - 12*$Octave
	$min_after_transpose := $Min
	message($min_after_transpose)
	else
	if ($Octave > 0)
	$max_after_transpose := $Max
	$min_after_transpose := $Min - 12*$Octave
	end if
	end if
end on

on note
    {Learn Key Range}
	if ($learn_button = 1)
		if ($learn_counter = 0)
			$Min := $EVENT_NOTE
			inc($learn_counter)
		else
			$Max := $EVENT_NOTE
			$learn_counter := 0
			$learn_button := 0
			
			if ($Min > $Max)
				$a := $Max
				$Max := $Min
				$Min := $a
			end if
		end if
		ignore_event($EVENT_ID)
		exit
	end if

	{Velocity to CC1}

        if (in_range($EVENT_NOTE,$min_after_transpose,$max_after_transpose))
            if ($V_C = 1)
                change_velo($EVENT_ID, %CC[1])
            end if
        end if

	{Transposition}

    if ($pitch_bypass = 0)
    $transposition := ($Octave * 12) + $Semi 
    end if
    $transposed_note := $EVENT_NOTE + $transposition
	
	if ($transposed_note < 0)
		$transposed_note := 0
	end if
	
	if ($transposed_note > 127)
		$transposed_note := 127
	end if

    if (in_range($EVENT_NOTE,$min_after_transpose,$max_after_transpose))
		ignore_event($EVENT_ID)

	{Timing}
	if ($time_bypass = 0)
        if ($n=0)
            $n:=1
            wait($1st_note*1000+1)
            else $n:=0
        end if
	end if
       
            change_note($EVENT_ID,$transposed_note)
        	$Delayed_Note := play_note ($transposed_note, $EVENT_VELOCITY, 0, -1)
    end if
    
end on

on release

    	if (in_range($EVENT_NOTE,$min_after_transpose,$max_after_transpose))
        ignore_event($EVENT_ID)
        note_off ($Delayed_Note)
        {Legato}
        while ($i<128)
            if (%KEY_DOWN[$i]=1)
                $pressed := 1
                $n := $n + 1
            end if
            inc($i)
        end while
        if ($pressed = 0)
            $n := 0
        end if
        $i := 0
        $pressed := 0
    end if
end on
